profile_trace() {
    if [ "$PROFILE_TRACE" = "1" ]; then
        echo "trace: $1"
    fi
}

if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    setopt xtrace prompt_subst
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# DO NOT use 'cat', because that spawns a process and we end up with 'cat's command line :)
if [[ $(uname) == "Linux" ]]; then
    if cat /proc/version | grep Microsoft >/dev/null 2>&1; then
        export WSL=1
    else
        export WSL=0
    fi
fi

# export the bin folder to the path
export PATH="$HOME/.dotnet:$HOME/.dotnet/tools:$HOME/.cargo/bin:$HOME/.dotfiles/bin:$PATH"

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

if [[ $(uname) == "Darwin" ]]; then
    export VAGRANT_DEFAULT_PROVIDER="parallels"
fi

# Set up library paths
export LIBRARY_PATH="$LIBRARY_PATH:/usr/local/lib"
export CPATH="$CPATH:/usr/local/include"

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
    profile_trace "sourcing $file"
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    profile_trace "sourcing $file"
    source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit -u -D
compinit -u -D

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
    profile_trace "sourcing $file"
    source $file
done

unset config_files

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi

source "$HOME/.dotfiles/functions/todo_completion"